plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.ojhdtapp.parabox"
        minSdk 26
        targetSdk 34
        versionCode 15
        versionName "1.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.7"
    }
    lintOptions {
        abortOnError false
    }
    aaptOptions {
        noCompress "tflite"
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }
    namespace 'com.ojhdtapp.parabox'

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) abi = "all"//兼容
            def version = variant.versionName
            outputFileName = "Parabox" +
                    "-${version}" +
                    "-${abi}" +
                    ".apk"
        }
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

androidComponents {
    onVariants(selector().withBuildType("release")) {
        it.packaging.resources.excludes.add("META-INF/**")
    }
    onVariants(selector().withBuildType("debug")) {
        it.packaging.resources.excludes.add("META-INF/**")
    }
}

kapt {
    correctErrorTypes = true
}

ksp {
    arg("compose-destinations.codeGenPackageName", "com.ojhdtapp.parabox")
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.core:core-google-shortcuts:1.1.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.animation:animation-graphics:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.9.0-alpha01'
    implementation 'androidx.test:core-ktx:1.5.0'
    implementation project(':Parabox Development Kit')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "com.google.android.material:material:1.10.0-rc01"

    runtimeOnly 'com.celeral:log4j2-android:1.0.0'

    implementation "org.jetbrains.kotlin:kotlin-serialization:1.9.21"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:1.5.0"

    // Map & Location
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.maps.android:maps-compose:2.11.4'
    implementation 'com.google.android.gms:play-services-maps:18.2.0'

    // DropdownMenu
    implementation "me.saket.cascade:cascade-compose:2.3.0"
    implementation "me.saket.telephoto:zoomable-image-coil:0.6.2"


    // swipe
    implementation "me.saket.swipe:swipe:1.2.0"

    // Onedrive
    implementation 'com.microsoft.identity.client:msal:4.2.0'

    // Qiniu
    implementation 'com.qiniu:qiniu-java-sdk:7.12.1'

    // Tencent COS
    implementation 'com.qcloud.cos:cos-android-lite-nobeacon:5.9.8'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:31.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation 'androidx.hilt:hilt-work:1.1.0'
    kapt 'androidx.hilt:hilt-compiler:1.1.0'

    // AppCompact
    def appcompat_version = "1.7.0-alpha03"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    // Google Drive Api
    implementation 'com.google.android.gms:play-services-auth:20.6.0'
    implementation('com.google.api-client:google-api-client-android:1.26.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    // Room DB backup
    implementation 'de.raphaelebner:roomdatabasebackup:1.0.0-beta10'

    // FontAwesome Icon
    implementation 'com.github.Gurupreet:FontAwesomeCompose:1.1.0'

    // Image Previewer
    implementation 'com.github.jvziyaoyao:ImageViewer:1.0.2-alpha.6'

    // ML-Kit
    implementation 'com.google.mlkit:entity-extraction:16.0.0-beta4'
    implementation 'com.google.mlkit:smart-reply:17.0.2'
    implementation 'com.google.mlkit:translate:17.0.1'
    implementation 'com.google.mlkit:language-id:17.0.4'

    // Extended Gestures
    implementation 'com.github.SmartToolFactory:Compose-Extended-Gestures:3.0.0'

    // Amplituda
    implementation 'com.github.lincollincol:amplituda:2.2.1'

    // Apache Commons
    implementation "commons-io:commons-io:2.11.0"

    // Development Kit
    implementation 'com.ojhdt:parabox-development-kit:1.0.6'

    // Paging
    def paging_version = "3.3.0-alpha02"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:$paging_version"

    // Coil
    implementation 'io.coil-kt:coil:2.4.0'
    implementation 'io.coil-kt:coil-gif:2.4.0'
    implementation 'io.coil-kt:coil-svg:2.4.0'
    implementation 'io.coil-kt:coil-compose:2.4.0'

    // Lottie
    def lottieVersion = "5.0.3"
    implementation "com.airbnb.android:lottie-compose:$lottieVersion"

    // Navigation Compose
    def nav_version = "2.7.6"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // Compose Destinations
    implementation 'io.github.raamcosta.compose-destinations:animations-core:1.9.56'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.9.56'

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Accompanist
    def acc_version = "0.32.0"
    implementation "com.google.accompanist:accompanist-permissions:$acc_version"
    implementation "com.google.accompanist:accompanist-placeholder-material3:$acc_version"

    // Window
    implementation "androidx.window:window:1.2.0-beta03"

    // M3
    implementation "androidx.compose.material3:material3-adaptive-android:1.0.0-alpha03"
    implementation "androidx.compose.material3:material3-adaptive-navigation-suite-android:1.0.0-alpha02"
    implementation "androidx.compose.material3:material3:1.2.0-beta01"
    implementation "androidx.compose.material3:material3-window-size-class:1.2.0-beta01"

    // Chrome Custom Tab
    implementation "androidx.browser:browser:1.5.0"

    // ViewModel
    def lifecycle_version = "2.6.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation("androidx.lifecycle:lifecycle-process:$lifecycle_version")

    // Coroutine Lifecycle Scopes
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"

    // Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.48.1"
    kapt "com.google.dagger:hilt-compiler:2.48.1"
//    implementation 'com.google.dagger:hilt-android:2.42'
//    kapt 'com.google.dagger:hilt-android-compiler:2.42'
//    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    // Room
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
}