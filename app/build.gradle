plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp' version '1.6.21-1.0.6'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.ojhdtapp.parabox"
        minSdk 26
        targetSdk 33
        versionCode 12
        versionName "1.0.10-beta"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.2.0-rc02"
    }
    packagingOptions {
        resources {
            exclude '/META-INF/{AL2.0,LGPL2.1}'
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.md'
            exclude 'META-INF/LICENSE-notice.md'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/notice.txt'
            exclude("META-INF/*.kotlin_module")
        }
    }
    lintOptions {
        abortOnError false
    }
    aaptOptions {
        noCompress "tflite"
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) abi = "all"//兼容
            def version = variant.versionName
            outputFileName = "Parabox" +
                    "-${version}" +
                    "-${abi}" +
                    ".apk"
            if (variant.buildType.name == "release") {
                variant.getPackageApplicationProvider().get().outputDirectory = new File(project.rootDir.absolutePath
                        + "/app/release")
            }
        }
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.core:core-google-shortcuts:1.1.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "com.google.android.material:material:1.8.0-beta01"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:31.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // AppCompact
    def appcompat_version = "1.7.0-alpha01"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    // Google Drive Api
    implementation 'com.google.android.gms:play-services-auth:20.3.0'
    implementation('com.google.api-client:google-api-client-android:1.26.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'guava-jdk5'
    }
    // Room DB backup
    implementation 'de.raphaelebner:roomdatabasebackup:1.0.0-beta10'

    // FontAwesome Icon
    implementation 'com.github.Gurupreet:FontAwesomeCompose:1.1.0'

    // Image Previewer
    implementation 'com.github.jvziyaoyao:ImageViewer:1.0.1-alpha.3'

    // ML-Kit
    implementation 'com.google.mlkit:entity-extraction:16.0.0-beta4'
    implementation 'com.google.mlkit:smart-reply:17.0.2'
    implementation 'com.google.mlkit:translate:17.0.1'
    implementation 'com.google.mlkit:language-id:17.0.4'

    // Extended Gestures
    implementation 'com.github.SmartToolFactory:Compose-Extended-Gestures:2.0.0'

    // Amplituda
    implementation 'com.github.lincollincol:amplituda:2.2.1'

    // Apache Commons
    implementation "commons-io:commons-io:2.7"

    // Development Kit
    implementation 'com.ojhdt:parabox-development-kit:1.0.6'

    // Paging
    def paging_version = "3.2.0-alpha03"
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.paging:paging-compose:1.0.0-alpha17"

    // Coil
    implementation 'io.coil-kt:coil:2.2.2'
    implementation 'io.coil-kt:coil-gif:2.2.0'
    implementation 'io.coil-kt:coil-svg:2.2.0'
    implementation 'io.coil-kt:coil-compose:2.2.2'

    // Shimmer
    implementation 'com.valentinilk.shimmer:compose-shimmer:1.0.3'

    // Lottie
    def lottieVersion = "5.0.3"
    implementation "com.airbnb.android:lottie-compose:$lottieVersion"

    // Compose Destinations
//    implementation 'io.github.raamcosta.compose-destinations:core:1.5.6-beta'
    implementation 'io.github.raamcosta.compose-destinations:animations-core:1.7.30-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.7.30-beta'

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Accompanist
    def acc_version = "0.26.1-alpha"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$acc_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$acc_version"
    implementation "com.google.accompanist:accompanist-permissions:$acc_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$acc_version"
    implementation "com.google.accompanist:accompanist-placeholder-material:$acc_version"

    // Navigation Compose
    def nav_version = "2.6.0-alpha04"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // m3
    implementation "androidx.compose.material3:material3:1.1.0-alpha03"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.0-alpha03"

    // Chrome Custom Tab
    implementation "androidx.browser:browser:1.4.0"

    // ViewModel
    def lifecycle_version = "2.6.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation("androidx.lifecycle:lifecycle-process:$lifecycle_version")

    // Coroutine Lifecycle Scopes
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    // Dagger - Hilt
    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-android-compiler:2.42'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Room
    def room_version = "2.5.0-rc01"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
}